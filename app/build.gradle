apply plugin: 'com.android.application'
apply from: "$project.rootDir/tools/script-git-version.gradle"

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        applicationId "info.santhosh.evlo"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode gitVersionCode
        versionName gitVersionName
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            versionNameSuffix " Debug"
        }

        release {
            shrinkResources true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'DATA_GOV_IN_API_KEY', MyDataGovInApiKey
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

ext {
    // version numbers
    junitVersion = '4.12'
    supportLibVersion = '26.0.1'
    constraintLayoutVersion = '1.0.2'
    okhttpVersion = '2.2.0'
    okhttp3Version = '3.8.1'
    simplexmlVersion = '2.7.1'
    stethoVersion = '1.4.1'
    leakCanaryVersion = '1.5.1'
    materialIntroVersion = 'v4.2.2'
    evernoteJobVersion = '1.1.11'
    stickyHeaderVersion = '0.2.8'
    protoModelVersion = '1.2-SNAPSHOT'
    multidexVersion = '1.0.1'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile "junit:junit:${junitVersion}"

    // support libs
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"

    // job scheduler background compatibility
    compile "com.evernote:android-job:${evernoteJobVersion}"

    // ui
    compile "ca.barrenechea.header-decor:header-decor:${stickyHeaderVersion}"
    compile "com.github.apl-devs:appintro:${materialIntroVersion}"

    // okhttp
//    compile "com.squareup.okhttp:okhttp:${okhttpVersion}"
    compile "com.squareup.okhttp3:okhttp:${okhttp3Version}"

    // for xml parsing
    compile("org.simpleframework:simple-xml:${simplexmlVersion}") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    // protobuf
    compile "com.github.santhoshvai:evlo-protobuf-model:${protoModelVersion}"

    // leak canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"

    // stetho - for inspecting networks and sqlite dbs
    debugCompile "com.facebook.stetho:stetho:${stethoVersion}"
}
