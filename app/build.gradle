apply plugin: 'com.android.application'
apply from: "$project.rootDir/tools/script-git-version.gradle"

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"

    defaultConfig {
        applicationId "info.santhosh.evlo"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode gitVersionCode
        versionName gitVersionName
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            versionNameSuffix " Debug"
        }

        release {
            shrinkResources true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'DATA_GOV_IN_API_KEY', MyDataGovInApiKey
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

ext{
    // version numbers
    junitVersion = '4.12'
    supportLibVersion = '25.3.1'
    constraintLayoutVersion = '1.0.2'
    okhttpVersion = '2.2.0'
    simplexmlVersion = '2.7.1'
    stethoVersion = '1.4.1'
    leakCanaryVersion = '1.5.1'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile "junit:junit:${junitVersion}"

    // support libs
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"

    // job scheduler background compatibility
    compile 'com.evernote:android-job:1.1.10'

    // ui
    compile "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"
    compile 'ca.barrenechea.header-decor:header-decor:0.2.8'

    // okhttp
    compile "com.squareup.okhttp:okhttp:${okhttpVersion}"

    // for xml parsing
    compile("org.simpleframework:simple-xml:${simplexmlVersion}"){
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    // protobuf
    compile 'com.github.santhoshvai:evlo-protobuf-model:1.0-SNAPSHOT'
    compile 'com.google.protobuf:protobuf-java:3.3.0'

    // leak canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"

    // stetho - for inspecting networks and sqlite dbs
    debugCompile "com.facebook.stetho:stetho:${stethoVersion}"
}
