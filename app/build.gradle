apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "info.santhosh.evlo"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            minifyEnabled true
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {

        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'DATA_GOV_IN_API_KEY', MyDataGovInApiKey
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

ext{
    // version numbers
    junitVersion = '4.12'
    supportLibVersion = '25.3.1'
    constraintLayoutVersion = '1.0.2'
    okhttpVersion = '2.2.0'
    simplexmlVersion = '2.7.1'
    stethoVersion = '1.4.1'
    ahbottomnavVersion = '2.0.6'
    fbdeviceyearclassVersion = '2.0.0'
    leakCanaryVersion = '1.5'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile "junit:junit:${junitVersion}"

    // support libs
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"

    // ui
    compile "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"
    compile "com.aurelhubert:ahbottomnavigation:${ahbottomnavVersion}"

    // okhttp
    compile "com.squareup.okhttp:okhttp:${okhttpVersion}"

    // for xml parsing
    compile("org.simpleframework:simple-xml:${simplexmlVersion}"){
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    // db device year class - switch off animations for older phones
    compile "com.facebook.device.yearclass:yearclass:${fbdeviceyearclassVersion}"

    // leak canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"

    // stetho - for inspecting networks and sqlite dbs
    debugCompile "com.facebook.stetho:stetho:${stethoVersion}"

}
