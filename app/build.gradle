apply plugin: 'com.android.application'
apply from: "$project.rootDir/tools/script-git-version.gradle"
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "info.santhosh.evlo"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode gitVersionCode
        versionName gitVersionName
        vectorDrawables.useSupportLibrary = true
        resConfigs "en"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            // TODO, signing keys
            storeFile file("release.keystore")
            storePassword "myPassword"
            keyAlias "myAlias"
            keyPassword "Something...."
        }
    }

    buildTypes {
        debug {
            versionNameSuffix " Debug"
        }

        release {
            signingConfig signingConfigs.debug // while release to play store, change this
            shrinkResources true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

ext {
    // version numbers
    junitVersion = '4.12'
    supportLibVersion = '27.0.2'
    constraintLayoutVersion = '1.0.2'
    okhttpVersion = '2.2.0'
    okhttp3Version = '3.8.1'
    simplexmlVersion = '2.7.1'
    stethoVersion = '1.4.2'
    leakCanaryVersion = '1.5.1'
    evernoteJobVersion = '1.2.0'
    stickyHeaderVersion = '0.2.8'
    protoModelVersion = '1.2-SNAPSHOT'
    crashlyticsVersion = '2.6.8'
    recyclerViewAnimatorsVersion = '2.2.7'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile "junit:junit:${junitVersion}"

    // support libs
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:customtabs:${supportLibVersion}"
    compile "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"


    // job scheduler background compatibility
    compile "com.evernote:android-job:${evernoteJobVersion}"

    // ui
    compile "ca.barrenechea.header-decor:header-decor:${stickyHeaderVersion}"

    // okhttp
    compile "com.squareup.okhttp3:okhttp:${okhttp3Version}"

    // for xml parsing
    compile("org.simpleframework:simple-xml:${simplexmlVersion}") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    // protobuf model and protobuf lite dependency
    compile "com.github.santhoshvai:evlo-protobuf-model:${protoModelVersion}"

    // crash reporting
    compile("com.crashlytics.sdk.android:crashlytics:${crashlyticsVersion}@aar") {
        transitive = true;
    }

    // leak canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"

    // stetho - for inspecting networks and sqlite dbs
    debugCompile "com.facebook.stetho:stetho:${stethoVersion}"
    compile "jp.wasabeef:recyclerview-animators:${recyclerViewAnimatorsVersion}"
}
